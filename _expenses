# -*- mode: shell-script; -*-
#compdef expenses

_expenses() {
    _arguments -C \
               '1: :->cmds' \
               '*:: :->cmd_args'

    case $state in
        cmds)
            local -a subcmds=(
                'category:category'
                'completion:completion'
                'expense:expense'
                'income:income'
                'summary:summary'
            )
            _describe -t commands "expenses command" subcmds
            ;;
        cmd_args)
            case $words[1] in
                category)
                    _arguments \
                        '-D[delete category]: :__expenses_category_ids' \
                        '-i[edit category]: :__expenses_category_ids'
                    ;;
                completion)
                    ;;
                expense)
                    _arguments \
                        '(- *)-D[delete record]: :__expenses_expense_ids' \
                        '(-D)-i[edit record]: :__expenses_expense_ids' \
                        '(-D)-c[category id]: :__expenses_category_ids' \
                        '(-D)-d[date YYYY-mm-dd\[ HH:MM:SS\]]: :__expenses_date'
                    ;;
                income)
                    _arguments \
                        '(- *)-D[delete record]: :__expenses_expense_ids' \
                        '(-D)-i[exit record]: :__expenses_expense_ids' \
                        '(-D)-c[category id]: :__expenses_category_ids' \
                        '(-D)-d[date YYYY-mm-dd\[ HH:MM:SS\]]: :__expenses_date'
                    ;;
                summary)
                    _arguments \
                        '-a[show all]' \
                        '-b[show balance]' \
                        '-e[show expense]' \
                        '-i[show income]'
                    ;;
            esac
            ;;
    esac
}

__expenses_category_ids() {
    local ids=(${(@f)"$(expenses export category)"})
    _values 'category id' $ids
}

__expenses_expense_ids() {
    local ids=(${(@f)"$(expenses export expense)"})
    _values 'expense id' $ids
}

__expenses_income_ids() {
    local ids=(${(@f)"$(expenses export income)"})
    _values 'expense id' $ids
}

__expenses_date() {
    local date=$(date '+%Y-%m-%d')
    _values 'date' "$date"
}

compdef _expenses expenses
